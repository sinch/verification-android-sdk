apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlinx-serialization'

def configFilePath = "./src/main/assets/config.json"
def configFileEmptyMessage = "You are building Sinch Android Verification Sample Application project that requires presence of a configuration file with your auhthorization data.\n" +
        "Create and place your config.json file inside the " +
        "assets folder (app/src/main/assets) and re-build the application\n" +
        "Example of config.json:\n" +
        "[{\n" +
        "  \"name\": \"<ENVIRONMENT NAME\",\n" +
        "  \"appKey\": \"<YOUR SINCH APP KEY>\",\n" +
        "  \"environment\": \"ocra.api.sinch.com\"\n" +
        "}]\n"

def requiredConfigFileParamsMissingMessage = "You are building Sinch Android Verification Sample Application project that requires your application specific authorization data (key).\n" +
        "Check config.json file located in app/src/main/assets folder and fill it with these values copied from Sinch portal website of your application.\n"

android {
    namespace "com.sinch.sinchverification"

    compileSdkVersion(Versions.compileSdk)

    buildFeatures {
        viewBinding true
        buildConfig = true
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    signingConfigs {
        release {
            keyAlias keyAliasSinch
            keyPassword keyPasswordSinch
            storeFile keyFileSinch
            storePassword storePasswordSinch
        }
    }

    sourceSets {
        main.res.srcDirs += "$project.rootDir/samples/common/res"
    }

    defaultConfig {
        applicationId "com.sinch.sinchverification"
        minSdkVersion(Versions.minSdk)
        // Keep target SDK at 32 to allow creating and logging to file located at Documents directory easily:
        // See https://stackoverflow.com/questions/73620790/android-13-how-to-request-write-external-storage
        targetSdkVersion 32
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    flavorDimensions "mode", "apienv"
    productFlavors {
        internal {
            dimension "mode"
        }
    }

    variantFilter { variant ->
        def names = variant.flavors*.name
        // To check for a certain build type, use variant.buildType.name == "<buildType>"
        if (names.contains("internal") && !names.contains("production")) {
            // Gradle ignores any variants that satisfy the conditions above.
            // In our case it we don't need any other flavors other then 'production' - endpoint can be changed manually
            setIgnore(true)
        }
    }

    buildTypes {
        release {
            debuggable true
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = "17"
    }

}

preBuild.doFirst {
    def configFile = file(configFilePath)
    if (!configFile.exists()) {
        ant.fail(configFileEmptyMessage)
    }
    if (configFile.text.contains("<YOUR APP KEY")) {
        ant.fail(requiredConfigFileParamsMissingMessage)
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(':library:verification-all')

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${Versions.kotlin}"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:${Versions.xSerialization}"
    implementation "androidx.appcompat:appcompat:${Versions.appCompat}"
    implementation "androidx.core:core-ktx:${Versions.ktx}"
    implementation "com.google.android.material:material:${Versions.material}"
    implementation "androidx.constraintlayout:constraintlayout:${Versions.constraintLayout}"
    implementation "androidx.navigation:navigation-fragment-ktx:${Versions.navigation}"
    implementation "androidx.navigation:navigation-ui-ktx:${Versions.navigation}"
    implementation "com.squareup.okhttp3:okhttp:${Versions.okhttp}"
    implementation "com.squareup.okhttp3:logging-interceptor:${Versions.okhttp}"
    implementation 'org.greenrobot:eventbus:3.3.1'
    implementation "androidx.preference:preference-ktx:1.2.1"

    debugImplementation 'com.facebook.flipper:flipper:0.247.0'
    debugImplementation 'com.facebook.flipper:flipper-network-plugin:0.247.0'
    debugImplementation 'com.facebook.soloader:soloader:0.12.0'

    releaseImplementation 'com.facebook.flipper:flipper-noop:0.247.0'

    testImplementation "junit:junit:${Versions.jUnit}"

    androidTestImplementation "androidx.test.ext:junit:${Versions.jUnitAndroidX}"
    androidTestImplementation "androidx.test.espresso:espresso-core:${Versions.espresso}"
}
