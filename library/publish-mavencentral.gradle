apply plugin: 'maven-publish'
apply plugin: 'signing'

group = 'com.sinch.android.sdk.verification'
version = bintrayVersion

afterEvaluate {
    publishing {
        publications {
            Production(MavenPublication) {
                from components.productionRelease

                groupId = 'com.sinch.android.sdk.verification'
                artifactId = bintrayName
                version = bintrayVersion

                pom {
                    name = pomName
                    description = pomDescription
                    url = 'https://www.sinch.com/products/apis/verification/'
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'alewoj'
                            name = 'Aleksander WÃ³jcik'
                            email = 'aleksander.wojcik@sinch.com'
                        }
                    }
                    scm {
                        connection = 'scm:git:git@github.com:sinch/verification-android-sdk.git'
                        developerConnection = 'scm:git:git@github.com:sinch/verification-android-sdk.git'
                        url = 'https://github.com/sinch/verification-android-sdk'
                    }
                }
            }
        }
    }
}

def signingKeyFile = findProperty('signingVerificationFile')
if (signingKeyFile != null) {
    signing {
        def signingKey = new File(signingKeyFile).text
        def signingPassword = findProperty('signingVerificationPassword')
        def signingKeyId = findProperty('signingVerificationKeyId')
        useInMemoryPgpKeys(signingKeyId, signingKey, signingPassword)
        sign publishing.publications
    }
} else {
    println "signingVerificationFile property is not set. Maven publications will not be signed"
}